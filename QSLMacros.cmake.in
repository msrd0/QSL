include(CMakeParseArguments)

function(QSL_COMPILE)
	
	set(options QTYPE)
	set(oneValueArgs TARGET)
	set(multiValueArgs FILES)
	
	cmake_parse_arguments(_QSLC "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
	
	if (_QSLC_QTYPE)
		set(_QSLC_QTYPE_FLAG "-q")
	else()
		set(_QSLC_QTYPE_FLAG "")
	endif()
	
	set(_QSLC_TIMESTAMP "${CMAKE_CURRENT_BINARY_DIR}/${_QSLC_TARGET}_qslc.timestamp")
	add_custom_command(OUTPUT "${_QSLC_TIMESTAMP}"
	                   COMMENT "Compiling ${_QSLC_FILES}"
                       COMMAND qslc ARGS ${_QSLC_QTYPE_FLAG} -d "${CMAKE_CURRENT_BINARY_DIR}" ${_QSLC_FILES}
					   COMMAND ${CMAKE_COMMAND} -E touch "${_QSLC_TIMESTAMP}"
					   DEPENDS ${_QSLC_FILES}
					   WORKING_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}"
					   VERBATIM)
	add_custom_target("${_QSLC_TARGET}_qslc" DEPENDS "${_QSLC_TIMESTAMP}")
	add_dependencies("${_QSLC_TARGET}" "${_QSLC_TARGET}_qslc")
	target_include_directories("${_QSLC_TARGET}" PRIVATE "${CMAKE_CURRENT_BINARY_DIR}" "${QSL_INCLUDE_DIRS}")
	target_link_libraries("${_QSLC_TARGET}" Qt5::Core Qt5::Sql qslcore qslvariant) 
	
endfunction()
